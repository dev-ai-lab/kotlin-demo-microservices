plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'io.ktor.plugin' version "$ktor_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'application'
}

group 'com.ktor.greenfield'
version '0.0.1'

application {
    mainClass = 'com.shop.userservice.UserApplicationKt'
}

repositories {
    mavenCentral()
    mavenLocal()
}

/*// Ensure consistent JVM target between Java and Kotlin compilation
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}*/

dependencies {
    // Core server + engine
    implementation "io.ktor:ktor-server-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-netty-jvm:$ktor_version"

    // Common Ktor features
    implementation "io.ktor:ktor-server-auth-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-auth-jwt-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-content-negotiation-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-html-builder-jvm:$ktor_version"
    // Additional server plugins used in code
    implementation "io.ktor:ktor-server-status-pages-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-sessions-jvm:$ktor_version"

    // Serialization integration for Ktor. In plugins it needs org.jetbrains.kotlin.plugin.serialization
    implementation("io.ktor:ktor-serialization-kotlinx-json-jvm:$ktor_version")
    // The actual JSON library
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0")

    // Logging
    implementation "ch.qos.logback:logback-classic:$logback_version"

    // Ktor client (optional)
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-apache-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"

    // Dependency Injection: Koin (It's similar to java's Spring Framework)
    implementation "io.insert-koin:koin-core:4.1.1"
    implementation "io.insert-koin:koin-ktor:4.1.1"
    implementation "io.insert-koin:koin-logger-slf4j:4.1.1"

    // Utilities
    //implementation "io.github.cdimascio:dotenv-kotlin:6.5.1"
    // https://mvnrepository.com/artifact/com.password4j/password4j
    implementation("com.password4j:password4j:1.8.4")

    // Tests
    //testImplementation "io.ktor:ktor-server-tests-jvm:$ktor_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation("io.mockk:mockk:1.14.6")

    implementation project(':common-utils')
}

kotlin {
    jvmToolchain(21) // Use Java 21 consistently
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions.jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
}

// Configuration for Integrtation Test in separate source set
// --- 1. Define new source set ---
sourceSets {
    integrationTest {
        kotlin {
            srcDir 'src/integrationTest/kotlin'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }

        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += output + compileClasspath
    }
}

// --- 2. Extend configurations ---
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

// --- 3. Create test task ---
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

// --- 4. Hook into `check` lifecycle (optional) ---
check.dependsOn integrationTest

